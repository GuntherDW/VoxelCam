plugins {
	id 'net.minecraftforge.gradle.tweaker-client' version '2.0.2'
}
archivesBaseName = 'mod_VoxelCam'
version = '1.4.0-mc1.8'
ext.buildnumber = '33'

minecraft {
	version = '1.8'
	runDir = 'run'
    tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'
	mappings = 'snapshot_20151111'
}
processResources {
	def toReplace = [version:project.version, buildnumber:project.buildnumber]
	inputs.properties toReplace
	from (sourceSets.main.resources.srcDirs) {
		include 'litemod.json'
		expand toReplace
	}
	from (sourceSets.main.resources.srcDirs) { exclude 'litemod.json' }
}
repositories.flatDir {dir 'liteloader'}
configurations.compile.transitive=false
dependencies {
    deobfProvided 'com.mumfrey:liteloader:1.8-SNAPSHOT:srgnames'
	provided project('voxellib')
	compile 'org.facebook4j:facebook4j-core:2.3.1'
	compile 'com.github.jreddit:jreddit:1.0.3'
	compile 'org.twitter4j:twitter4j-core:4.0.4'
	compile 'com.googlecode.json-simple:json-simple:1.1.1'
}
task litemod(type:Jar, dependsOn: [reobfJar, {project('voxellib').reobfJar}]) {
	extension 'litemod'
	from zipTree(jar.archivePath)
    from {project('voxellib').jar.archivePath}
	from(configurations.compile.collect {it.isDirectory() ? it : zipTree(it)}) {
		exclude 'META-INF/***', 'examples/**'
	}
}
artifacts {litemod}