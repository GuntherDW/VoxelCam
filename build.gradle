plugins {
	id 'net.minecraftforge.gradle.tweaker-client' version '2.0.2'
	id 'com.github.johnrengelman.shadow' version '1.2.2'
}
archivesBaseName = 'mod_VoxelCam'
version = '1.4.0-mc1.8'
ext.buildnumber = '33'

minecraft {
	version = '1.8'
	runDir = 'run'
    tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'
	mappings = 'snapshot_20151111'
}
processResources {
	def toReplace = [version:project.version, buildnumber:project.buildnumber]
	inputs.properties toReplace
	from (sourceSets.main.resources.srcDirs) {
		include 'litemod.json'
		expand toReplace
	}
	from (sourceSets.main.resources.srcDirs) { exclude 'litemod.json' }
}
repositories.flatDir {dir 'liteloader'}
configurations {
	compile.extendsFrom shadow
}
dependencies {
    deobfProvided 'com.mumfrey:liteloader:1.8-SNAPSHOT:srgnames'
	provided project('voxellib')
	shadow 'org.apache.httpcomponents:httpmime:4.3.3' // for multipart entity
	shadow 'oauth.signpost:signpost-core:1.2.1.2'
	shadow 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
}
shadowJar {
	dependencies {
		// will include jar later
		exclude project('voxellib')
		// included in mc
		exclude dependency('org.apache.httpcomponents:httpclient')
		exclude dependency('org.apache.httpcomponents:httpcore')
		exclude dependency('commons-logging:commons-logging')
		exclude dependency('commons-codec:commons-codec')
	}
	relocate 'org.apache.http.entity.mime', 'com.thatapplefreak.redist.org.apache.http.entity.mime'
	relocate 'com.google.gdata', 'com.thatapplefreak.redist.com.google.gdata'
	relocate 'oauth', 'com.thatapplefreak.redist.oauth'
}
reobf {shadowJar{}}
task litemod(type:Jar, dependsOn: [reobfShadowJar, {project('voxellib').reobfJar}]) {
	extension 'litemod'
	from zipTree(shadowJar.archivePath)
    from {project('voxellib').jar.archivePath}
	exclude 'dummyThing'
}
artifacts.archives litemod, shadowJar
